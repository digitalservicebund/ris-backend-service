plugins {
  id "org.springframework.boot" version "2.7.0"
  id "io.spring.dependency-management" version "1.1.0"
  id "java"
  id "com.diffplug.spotless" version "6.11.0"
  id "jacoco"
  id "org.sonarqube" version "3.4.0.2513"
  id "com.github.jk1.dependency-license-report" version "2.1"
  id "com.gorylenko.gradle-git-properties" version "2.4.1"
  id "com.adarshr.test-logger" version "3.2.0"
}

group = "de.bund.digitalservice"
version = "0.0.1-SNAPSHOT"
sourceCompatibility = "17"

configurations { compileOnly { extendsFrom annotationProcessor } }

repositories {
  mavenCentral()
  maven {
    url = uri("https://maven.pkg.github.com/digitalservicebund/neuris-juris-xml-export")
    credentials {
      username = System.getenv("GH_PACKAGES_REPOSITORY_USER")
      password = System.getenv("GH_PACKAGES_REPOSITORY_TOKEN")
    }
  }
}

jacoco { toolVersion = "0.8.8" }

springBoot { buildInfo() }

testlogger { theme "mocha" }

dependencies {
  implementation "org.springframework.boot:spring-boot-starter-actuator"
  implementation "org.springframework.boot:spring-boot-starter-security"
  implementation("org.springframework.boot:spring-boot-starter-webflux") {
    exclude group: "io.netty", module: "netty-tcnative-classes"
    because("CVE-2021-43797, not using Tomcat")
  }
  implementation "org.springframework.cloud:spring-cloud-starter-kubernetes-client-config:2.1.3"
  // => CVE-2021-37136, CVE-2021-37137, CVE-2021-43797
  implementation("io.netty:netty-all:4.1.77.Final") {
    exclude group: "io.netty", module: "netty-tcnative-classes"
    because("CVE-2021-43797, not using Tomcat")
  }
  implementation("org.yaml:snakeyaml:1.33") { because("CVE-2022-25857") }
  implementation "ch.qos.logback:logback-classic:1.4.4"
  implementation "ch.qos.logback:logback-core:1.4.0"
  implementation "org.springframework.boot:spring-boot-starter-data-r2dbc"
  implementation "io.r2dbc:r2dbc-postgresql:0.8.12.RELEASE"
  implementation "io.r2dbc:r2dbc-spi:1.0.0.RELEASE"
  implementation "io.r2dbc:r2dbc-pool:0.9.1.RELEASE"

  implementation "com.sendinblue:sib-api-v3-sdk:6.0.0"

  implementation platform("software.amazon.awssdk:bom:2.17.191")
  implementation "software.amazon.awssdk:netty-nio-client"
  implementation "software.amazon.awssdk:s3"
  implementation "org.springdoc:springdoc-openapi-webflux-ui:1.6.9"

  // CVE-2022-42004, CVE-2022-42003
  implementation "com.fasterxml.jackson:jackson-bom:2.14.0-rc1"
  implementation "com.fasterxml.jackson.core:jackson-databind:2.14.0-rc1"

  implementation "org.docx4j:docx4j-JAXB-MOXy:11.4.8"
  implementation "com.sun.mail:jakarta.mail:2.0.1"
  implementation "com.sun.activation:jakarta.activation:2.0.1"
  implementation("jakarta.xml.bind:jakarta.xml.bind-api") { version { strictly "3.0.1" } }
  implementation "org.freehep:freehep-graphicsio-emf:2.4"

  // package served by private repo, requires authentication
  implementation "de.bund.digitalservice:neuris-juris-xml-export:0.3.6"
  // for local development (both):
  //  implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.13.3"
  //  implementation files("../neuris-juris-xml-export/build/libs/neuris-juris-xml-export-0.1.5.jar")

  implementation "io.micrometer:micrometer-registry-prometheus:1.9.3"
  implementation "io.sentry:sentry-spring-boot-starter:6.5.0"
  implementation "org.springframework.boot:spring-boot-starter-validation"

  compileOnly "org.projectlombok:lombok"
  annotationProcessor "org.projectlombok:lombok"

  developmentOnly "org.springframework.boot:spring-boot-devtools"

  testImplementation("org.springframework.boot:spring-boot-starter-test") {
    exclude group: "org.mockito", module: "mockito-core"
  }
  testImplementation "org.mockito:mockito-inline:4.8.0"
  testImplementation "io.projectreactor:reactor-test"
  testImplementation "org.springframework.security:spring-security-test"
  testImplementation "com.tngtech.archunit:archunit-junit5:1.0.0"
  testImplementation "org.mockito:mockito-junit-jupiter:4.8.0"
  testImplementation "org.testcontainers:testcontainers:1.17.3"
  testImplementation "org.testcontainers:junit-jupiter:1.17.3"
  testImplementation "org.testcontainers:postgresql:1.17.3"
}

jar { // We have no need for the plain archive, thus skip creation for build speedup!
  enabled = false }

test {
  useJUnitPlatform { excludeTags "integration", "journey" }
}

task integrationTest(type: Test) {
  description = "Runs the integration tests."
  group = "verification"
  useJUnitPlatform { includeTags "integration" }

  // So that running integration test require running unit tests first,
  // and we won"t even attempt running integration tests when there are
  // failing unit tests.
  dependsOn test
  finalizedBy jacocoTestReport
}
check.dependsOn integrationTest

task journeyTest(type: Test) {
  description = "Runs the journey tests."
  group = "verification"
  useJUnitPlatform { includeTags "journey" }
}

jacocoTestReport {
  // Jacoco hooks into all tasks of type: Test automatically, but results for each of these
  // tasks are kept separately and are not combined out of the box.. we want to gather
  // coverage of our unit and integration tests as a single report!
  executionData fileTree(project.buildDir.absolutePath).include("jacoco/*.exec")
  reports {
    xml.required = true
    html.required = true
  }
  dependsOn integrationTest // All tests are required to run before generating a report..

  afterEvaluate {
    classDirectories.setFrom(files(classDirectories.files.collect {
      fileTree(dir: it, exclude: ["**/config/*"])
    }))
  }
}

spotless {
  java {
    removeUnusedImports()
    googleJavaFormat()
    custom "Refuse wildcard imports", {
      // Wildcard imports can't be resolved by spotless itself.
      // This will require the developer themselves to adhere to best practices.
      if (it =~ /\nimport .*\*;/) {
        throw new AssertionError("Do not use wildcard imports. \"spotlessApply\" cannot resolve this issue.")
      }
    }
  }
  groovyGradle {
    greclipse("4.6.3").configFile("greclipse.properties")
    toggleOffOn()
    replaceRegex("quotes", "(['])(.*)\\1", "\"\$2\"")
  }
  format "misc", {
    target "**/*.js", "**/*.json", "**/*.md", "**/*.properties", "**/*.sh", "**/*.sql", "**/*.yaml", "**/*.yml"
    targetExclude "web/**", // Has its own prettier setup!
        "**/dist/**", "**/static/**"
    // spotless:off
    prettier(["prettier": "2.6.1", "prettier-plugin-properties": "0.1.0", "prettier-plugin-sh": "0.7.1", "prettier-plugin-sql": "0.10.0"])
        .config([
          "keySeparator": "=", // for prettier-plugin-properties
          "language": "postgresql", // for prettier-plugin-sql
        ])
    // spotless:on
  }
}

sonarqube {
  // NOTE: sonarqube picks up combined coverage correctly without further configuration from:
  // build/reports/jacoco/test/jacocoTestReport.xml
  properties {
    property "sonar.projectKey", "digitalservicebund_ris-backend-service"
    property "sonar.organization", "digitalservicebund"
    property "sonar.host.url", "https://sonarcloud.io"
    property "sonar.coverage.exclusions", "**/config/**,**/S3AsyncMockClient.java"
  }
}

project.tasks["sonarqube"].dependsOn jacocoTestReport

bootBuildImage {
  def containerRegistry = System.getenv("CONTAINER_REGISTRY") ?: "ghcr.io"
  def containerImageName = System.getenv("CONTAINER_IMAGE_NAME") ?: "digitalservicebund/${rootProject.name}"
  def containerImageVersion = System.getenv("CONTAINER_IMAGE_VERSION") ?: "latest"

  imageName = "${containerRegistry}/${containerImageName}:${containerImageVersion}"
  builder = "paketobuildpacks/builder@sha256:0e0cdb719946ed6accc63da32f58e9853575b7b7e1b2110b08406d01809423ee" // pin to version 0.1.260-tiny
  publish = false
  docker {
    publishRegistry {
      username = System.getenv("CONTAINER_REGISTRY_USER") ?: ""
      password = System.getenv("CONTAINER_REGISTRY_PASSWORD") ?: ""
      url = "https://${containerRegistry}"
    }
  }
}


import com.github.jk1.license.filter.LicenseBundleNormalizer

licenseReport {
  // If there's a new dependency with a yet unknown license causing this task to fail
  // the license(s) will be listed in build/reports/dependency-license/dependencies-without-allowed-license.json
  allowedLicensesFile = new File("$projectDir/allowed-licenses.json")
  filters = [
    // With second arg true we get the default transformations:
    // https://github.com/jk1/Gradle-License-Report/blob/7cf695c38126b63ef9e907345adab84dfa92ea0e/src/main/resources/default-license-normalizer-bundle.json
    new LicenseBundleNormalizer("$projectDir/license-normalizer-bundle.json", true)
  ]
}
